%%%
   VERSION: 1
   LANGUAGE: ENGLISH
%%%

MODULE Irbp1EEv(SYSMODULE)
  !---------------------------------------------------------------------------!
  !
  ! (c) ABB
  !
  !---------------------------------------------------------------------------!
  !
  ! Module:       IrbpC1EEv.sys
  !
  ! Written by:   Stefan Öhrn
  !
  ! Description:  cell specifik tasks for irbp C 1.
  !
  ! Procedures:
  !
  ! Functions:
  !
  ! Created:      2005-08-22
  !
  ! Version:      1.0.0
  !
  ! History:      1.0.0
  !                 First release
  !               2005-11-04 SÖ
  !                 Major changes.
  !               2006-11-03 SÖ
  !                 Added subscription to new event EE_ABORT.
  !                  Deactivates activated MECH after aborted cycle.
  !                 Added subscription to event EE_START.
  !                  Deactivates all activated MECHs.
  !
  !---------------------------------------------------------------------------!
  !
  LOCAL CONST byte GAP_STN_1:=1;
  LOCAL CONST byte GAP_STN_2:=2;
  LOCAL VAR num nActMem;
  !
  CONST ee_event peEeAbortDeact1:=[EE_ABORT,"EvAbortDeact1","",255,255];
  CONST ee_event peEeAbortDeactA1:=[EE_START,"EvAbortDeactAll1","",255,255];
  ! pre part events
  CONST ee_event peEvIndexToStn1:=[EE_INDEX,"EvIndexToStn1","",1,GAP_STN_1];
  CONST ee_event peEvIndexToStn2:=[EE_INDEX,"EvIndexToStn2","",1,GAP_STN_2];
  !
  ! part running
  !
  ! post part events
  !
  PROC EvAbortDeact1(\switch dummy)
    IF (nActMem>0) THEN
      IF (nActMem=3) THEN
        DeactInterch1;
      ENDIF
      nActMem:=0;
    ENDIF
  ENDPROC

  PROC EvAbortDeactAll1(\switch dummy)
    VAR jointtarget jtCurrent;

    IF RobOs() THEN
      DeactInterch1;
    ELSE
      ActInterch1;
      jtCurrent:=CJointT();
      IF (jtCurrent.extax.eax_d <= 1) THEN
        SetDO soLS_1_INPOS,1;
        SetDO soLS_2_INPOS,0;
      ELSEIF (jtCurrent.extax.eax_d >= 179) THEN
        SetDO soLS_1_INPOS,0;
        SetDO soLS_2_INPOS,1;
      ELSE
        IndexToStn1;
      ENDIF
      DeactInterch1;
    ENDIF
  ENDPROC

  PROC EvIndexToStn1(\switch dummy)
    IF (OpMode()<>OP_AUTO) TPWrite "Indexing to station 1";
    ActInterch1;
    nActMem:=3;
    IndexToStn1;
    DeactInterch1;
    nActMem:=0;
  ENDPROC

  PROC EvIndexToStn2(\switch dummy)
    IF (OpMode()<>OP_AUTO) TPWrite "Indexing to station 2";
    ActInterch1;
    nActMem:=3;
    IndexToStn2;
    DeactInterch1;
    nActMem:=0;
  ENDPROC
ENDMODULE